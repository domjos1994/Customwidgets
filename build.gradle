/*
 * Copyright (C) 2017-2020  Dominic Joas
 *
 *  This program is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU General Public License
 *  as published by the Free Software Foundation; either version 3
 *  of the License, or (at your option) any later version.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def androidVersion = "1.1.0"
def junitVersion = "4.12"
def espressoVersion = "3.2.0"

def compVersion = 29
def minVersion = 20

def versionC = 15
def versionN = "2.3.11"
def pName = "customwidgets"

def javaLevel = 8

android {
    compileSdkVersion compVersion

    defaultConfig {
        minSdkVersion minVersion
        targetSdkVersion compVersion
        versionCode versionC
        versionName versionN

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        targetCompatibility = javaLevel
        sourceCompatibility = javaLevel
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:${androidVersion}"
    implementation "androidx.recyclerview:recyclerview:${androidVersion}"
    implementation "androidx.preference:preference:${androidVersion}"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation "com.google.android.material:material:${androidVersion}"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Test-Libraries
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(new File(projectDir, "../../Libraries/customwidgets/github.properties")))

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'de.domjos'
            artifactId pName
            version versionN
            artifact("$buildDir/outputs/aar/" + pName + "-debug.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/domjos1994/Customwidgets")
            credentials {
                username = githubProperties['gpr.usr']
                password = githubProperties['gpr.key']
            }
        }
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}
